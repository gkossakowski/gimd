<!--
Copyright (C) 2009 The Android Open Source Project

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-->


<!-- This is grammar defintion expressed with EBNF notation -->

ident = identF, { identN } ;
identF = letter ;
identN = letter | digit | '_' ;

space = ' ' ;
whitespace = space | LfCh ;

message = { field, LfCh } ;
nameValueSeparator = ' ' ;
field = { whitespace }, ident, nameValueSeparator, value ;

value = ( '<', LfCh, message, '>' ) | quotedString | numeric | bareString ;

quotedString = '"', { escaped | allowedChars }, '"' ;
escaped = '\"' | '\\' | hexEscaped ;
hexEscaped = '\x', hexDigit, hexDigit ;
hexDigit = 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | digit ;
allowedChars = ? java.lang.Character ? - ( '\' | '"' ) ;

bareString = { allowedChars - ( '\' | '"' | LfCh ) }

digit = '0' | positiveDigit ;
positiveDigit = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;
numeric = '0' | [ '-' ], '0.', ([ '0' ], positiveDigit), { [ '0' ], positiveDigit }
          | [ '-' ], positiveDigit, { digit }, '.', ([ '0' ], positiveDigit), { [ '0' ], positiveDigit }

LfCh = ? TO BE DEFINED SOMEHOW ?
